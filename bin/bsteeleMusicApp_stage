#!/bin/tcsh
#set echo; set verbose

echo $0\:

set debug=0
#   default is beta:
set isBeta=1
set app=bsteeleMusicApp
set flavor=beta
set git=~/github/bsteele_music_flutter
set bsteeleMusicLib=~/github/bsteeleMusicLib

while ( $#argv > 0 )
	switch ( $1 )
	case '-a':
	case '-alpha':
		set isBeta=1
		set flavor=alpha
		shift
		continue;
	case '-beta':
		set isBeta=1
		shift
		continue;
	case '-d':		#	debug
	case '-debug':
		set debug=1
		shift
		continue;
	default:
		echo unknown argument: $1
		exit -1;
		breaksw;
	endsw

	break;
end


if ( $debug != 0 ) then
	set echo; set verbose
endif

gcloud --quiet components update

#	test
cd $git
flutter test test
if ( $status != 0 ) exit -1

#	beta to local static host
set betaDir=/var/www/html/public_html/bsteeleMusicApp/$flavor

##	build the release version of the beta
flutter clean
date --utc +%Y%m%d_%H%M%S > lib/assets/utcDate.txt
#flutter config --enable-web
#   canvaskit fixes rendering errors, included in app for the park
flutter build web --release --web-renderer=canvaskit

# install beta lib util
cd bsteeleMusicLib
bin/bsteeleMusicUtil_install
cd $git

#	clear the last one
mkdir -p $betaDir
rm  -rf $betaDir/*

#	copy to localhost website
cp -r build/web/* $betaDir


#	send to flavor's web location
set gs=gs://www.bsteele.com/$app/$flavor
bsteele_auth
gsutil -m -q rsync -d -R \
    -x 'allSongs\.songlyrics$' \
    -x 'allSongs\.songmetadata$' \
    -x 'allSongPerformances\.songperformances$' \
    $betaDir/ $gs 	#	source needs the /
gsutil -m -q setmeta -h "Cache-Control:public, max-age=60" -r $gs
gsutil -m -q acl -r ch -u AllUsers:R $gs

echo test http://www.bsteele.com/$app/$flavor

#	deploy to local tomcat
cd $git
bin/bsteeleMusicApp_beta_war

# print some facts
bin/bsteeleMusicApp_codeLineCount
cat lib/assets/utcDate.txt
