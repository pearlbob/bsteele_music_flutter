#!/bin/tcsh
#set echo; set verbose

echo $0\:

sudo echo sudo required for local app server install: 

set debug=0
#   default is beta:
set isBeta=1
set app=bsteeleMusicApp
set flavor=beta
set git=~/github/bsteele_music_flutter
set bsteeleMusicLib=~/github/bsteeleMusicLib
set allSongs=~/github/allSongs.songlyrics

while ( $#argv > 0 )
	switch ( $1 )
	case '-a':
	case '-alpha':
		set isBeta=1
		set flavor=alpha
		shift
		continue;
	case '-beta':
		set isBeta=1
		shift
		continue;
	case '-d':		#	debug
	case '-debug':
		set debug=1
		shift
		continue;
	default:
		echo unknown argument: $1
		exit -1;
		breaksw;
	endsw

	break;
end


if ( $debug != 0 ) then
	set echo; set verbose
endif

#done by arch: gcloud components update

#	test
cd $git
echo test flutter app
flutter test test >& /dev/null
if ( $status != 0 ) then
	echo flutter test at $git failed: $status
	exit $status
endif

#	beta to local static host
set rootDir=/srv/http/public_html/bsteeleMusicApp
set betaDir=$rootDir/$flavor

# 
echo test the library
cd $bsteeleMusicLib/bsteele_music_lib
dart run test >& /dev/null
if ( $status != 0 ) then
	echo library tests failed.
	exit -1
endif
cd $git

##	build the web version of the beta
flutter clean

date --utc +%Y%m%d_%H%M%S > lib/assets/utcDate.txt
#flutter config --enable-web
set base=/bsteeleMusicApp/
if ( $isBeta ) set base=/public_html${base}beta/
if ( $debug ) then
	#flutter build web --profile --base-href $base --dart-define=Dart2jsOptimization=O0
	flutter build web --base-href $base 	#	fixme: temp
else
	flutter build web --base-href $base --release
endif
bsteeleMusicApp_canvaskit # offline canvas kit update

#	update the release notes
bin/bsteeleMusicApp_release_notes
if ( $status != 0 ) exit -1

# install beta lib util
cd bsteeleMusicLib
bin/bsteeleMusicUtil_install
if ( $status != 0 ) exit -1
cd $git

#	clear the last one
mkdir -p $betaDir
rm  -rf $betaDir/*

#	copy to localhost website
cp -r build/web/* $betaDir
cp $allSongs/allSong* $rootDir  # include song data

##	build the web cloud version of the beta
set base=/bsteeleMusicApp/
if ( $isBeta ) set base=${base}beta/
if ( $debug ) then
	flutter build web --base-href $base 	#	fixme: temp
	if ( $status != 0 ) exit -1
else
	flutter build web --base-href $base --release
	if ( $status != 0 ) exit -1
endif

#	send to flavor's web location
set gs=gs://www.bsteele.com/$app/$flavor
bsteele_auth
gcloud storage rsync build/web $gs -R --quiet --verbosity=error \
	-x "\..*/.*|\..*|.*/allSong.*" \
	--delete-unmatched-destination-objects \
	--predefined-acl=publicRead \
	--cache-control=public,max-age=60
if ( $status != 0 ) exit -1

echo test http://www.bsteele.com/$app/$flavor

#	deploy to local tomcat
cd $git
bin/bsteeleMusicApp_beta_war

# print some facts
bin/bsteeleMusicApp_codeLineCount
cat lib/assets/utcDate.txt
